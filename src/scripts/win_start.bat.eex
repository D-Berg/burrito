@echo off
setlocal enabledelayedexpansion

set RELEASE_ROOT=%~dp0\..

if not defined RELEASE_NAME (set RELEASE_NAME=<%= release_name %>)
if not defined RELEASE_VSN (for /f "tokens=1,2" %%K in ('type "!RELEASE_ROOT!\releases\start_erl.data"') do (set ERTS_VSN=%%K) && (set RELEASE_VSN=%%L))
if not defined RELEASE_MODE (set RELEASE_MODE=embedded)

set REL_VSN_DIR=!RELEASE_ROOT!\releases\!RELEASE_VSN!
call "!REL_VSN_DIR!\env.bat"

if not defined RELEASE_COOKIE (set /p RELEASE_COOKIE=<!RELEASE_ROOT!\releases\COOKIE)
if not defined RELEASE_NODE (set RELEASE_NODE=!RELEASE_NAME!)
if not defined RELEASE_TMP (set RELEASE_TMP=!RELEASE_ROOT!\tmp)
if not defined RELEASE_VM_ARGS (set RELEASE_VM_ARGS=!REL_VSN_DIR!\vm.args)
if not defined RELEASE_DISTRIBUTION (set RELEASE_DISTRIBUTION=sname)
if not defined RELEASE_BOOT_SCRIPT (set RELEASE_BOOT_SCRIPT=start)
if not defined RELEASE_SYS_CONFIG (set RELEASE_SYS_CONFIG=!REL_VSN_DIR!\sys)

set "REL_EXEC=elixir" && set "REL_EXTRA=--no-halt" && set "REL_GOTO=start"

if not "!REL_GOTO!" == "" (
  findstr "RUNTIME_CONFIG=true" "!RELEASE_SYS_CONFIG!.config" >nul 2>&1 && (
    set DEFAULT_SYS_CONFIG=!RELEASE_SYS_CONFIG!
    for /f "skip=1" %%X in ('wmic os get localdatetime') do if not defined TIMESTAMP set TIMESTAMP=%%X
    set RELEASE_SYS_CONFIG=!RELEASE_TMP!\!RELEASE_NAME!-!RELEASE_VSN!-!TIMESTAMP:~0,11!-!RANDOM!.runtime
    mkdir "!RELEASE_TMP!" >nul 2>&1
    copy /y "!DEFAULT_SYS_CONFIG!.config" "!RELEASE_SYS_CONFIG!.config" >nul || (
      echo Cannot start release because it could not write to "!RELEASE_SYS_CONFIG!.config"
      exit /b
    )
  )

  goto !REL_GOTO!
)

:start
if "!RELEASE_DISTRIBUTION!" == "none" (
  set RELEASE_DISTRIBUTION_FLAG=
) else (
  set RELEASE_DISTRIBUTION_FLAG=--!RELEASE_DISTRIBUTION! "!RELEASE_NODE!"
)

"!REL_VSN_DIR!\!REL_EXEC!.bat" !REL_EXTRA! ^
  --cookie "!RELEASE_COOKIE!" ^
  !RELEASE_DISTRIBUTION_FLAG! ^
  --erl "-mode !RELEASE_MODE!" ^
  --erl-config "!RELEASE_SYS_CONFIG!" ^
  --boot "!REL_VSN_DIR!\!RELEASE_BOOT_SCRIPT!" ^
  --boot-var RELEASE_LIB "!RELEASE_ROOT!\lib" ^
  --vm-args "!RELEASE_VM_ARGS!" -- %*